apply plugin: 'org.jetbrains.kotlin.multiplatform'

apply plugin: 'com.android.library'

android {
    compileSdkVersion(28)

    defaultConfig {
        minSdkVersion(21)
        targetSdkVersion(28)
    }

    // By default the android gradle plugin expects to find the kotlin source files in
    // the folder `main` and the test in the folder `test`. This is to be able place
    // the source code files inside androidMain and androidTest folders
    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
            java.srcDirs = ['src/androidMain/kotlin']
            res.srcDirs = ['src/androidMain/res']
        }
        test {
            java.srcDirs = ['src/androidTest/kotlin']
            res.srcDirs = ['src/androidTest/res']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

kotlin {
    targets {
        fromPreset(presets.android, 'android')


        def iosPreset = presets.iosX64
        fromPreset(iosPreset, 'ios') {
            binaries {
                framework {
                    // Disable bitcode embedding for the simulator build.
                    if (iosPreset == presets.iosX64) {
                        embedBitcode("disable")
                    }
                }
            }
            //compilations.main.outputKinds('FRAMEWORK')
            compilations.each {
                it.extraOpts("-linkerOpts", "-lsqlite3")
            }
        }

    }

    sourceSets {
        commonMain.dependencies {
            api 'org.jetbrains.kotlin:kotlin-stdlib-common'

            // COROUTINES
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.3.0"

            // MOKO - MVVM
            implementation "dev.icerock.moko:mvvm:0.3.1"

            // KOTLIN LOGGING
            //implementation "com.github.florent37:multiplatform-log:1.0.1"

            // KODE IN
            implementation "org.kodein.di:kodein-di-core:$kodeinVersion"
            implementation "org.kodein.di:kodein-di-erased:$kodeinVersion"
        }

        androidMain.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
            implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"

            // COROUTINES
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0"

            // MOKO - MVVM
            implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"

            // SERIALIZATION
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serializer_version"

            // KTOR
            implementation "io.ktor:ktor-client-android:$ktor_version"

            // LOG
            //implementation "com.github.florent37:multiplatform-log-android:1.0.1"
        }

        iosMain.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"

            // COROUTINE
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.3.0"

            // SERIALIZATION
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serializer_version"

            // KTOR
            implementation "io.ktor:ktor-client-ios:$ktor_version"

            // LOG
            //implementation "com.github.florent37:multiplatform-log-ios:1.0.1"
        }
    }
}

task packForXCode {
    def buildType = project.findProperty("kotlin.build.type") ?: "DEBUG"
    dependsOn "link${buildType.toLowerCase().capitalize()}FrameworkIos"
    //dependsOn "linkMainDebugFrameworkIOS"

    doLast {
        def srcFile = kotlin.targets.ios.binaries.getFramework(buildType).outputFile
        def targetDir = getProperty("configuration.build.dir")
        copy {
            from srcFile.parent
            into targetDir
            include 'SharedCode.framework/**'
            include 'SharedCode .framework.dSYM'
        }
    }
}